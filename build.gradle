buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

defaultTasks 'build'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.airbnb'
description = 'StatsD Support for Kafka Metrics (kafka version: 0.8.1 and 0.8.2)'

version = '1.0'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/airbnb/jars" }
}

dependencies {
    compile 'com.indeed:java-dogstatsd-client:2.0.10'

    compile 'org.apache.kafka:kafka_2.10:0.8.2.1'

    compile 'org.slf4j:slf4j-log4j12:+'

    testCompile('junit:junit:4.11',
            'org.easymock:easymock:3.2',
            "org.mockito:mockito-core:1.+")
}

configurations {
    //manually excludes some unnecessary dependencies
    compile.exclude module: 'zookeeper'
    compile.exclude module: 'zkclient'
    compile.exclude module: 'javax'
    compile.exclude module: 'jline'
    compile.exclude module: 'jms'
    compile.exclude module: 'jmxri'
    compile.exclude module: 'jmxtools'
    compile.exclude module: 'mail'
}

shadowJar {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/maven/*'
    dependencies {
        exclude(dependency('org.apache.kafka:kafka_2.10'))
    }
}

